/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-gradle-plugin'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.external-publish-custom'

java {
    sourceCompatibility JavaVersion.VERSION_1_8
}

dependencies {
  implementation gradleApi()
  implementation 'org.apache.commons:commons-compress'
  implementation 'commons-io:commons-io'
  implementation 'com.palantir.sls.versions:sls-versions'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.mockito:mockito-core'
  testImplementation 'junit:junit'
  testImplementation 'com.spotify:docker-client'
  testImplementation gradleTestKit()
}

task prepareDockerBundleDir(type: Zip) {
  from("${rootDir}/resource-managers/kubernetes/docker/src/main/dockerfiles/spark/Dockerfile") {
    into 'kubernetes/dockerfiles/spark'
    rename 'Dockerfile', 'Dockerfile.original'
  }

  from("${rootDir}/resource-managers/kubernetes/docker/src/main/dockerfiles/spark/entrypoint.sh") {
    into 'kubernetes/dockerfiles/spark'
  }

  from(fileTree("${rootDir}/bin")) {
    into 'bin'
  }
  from(fileTree("${rootDir}/sbin")) {
    into 'sbin'
  }
  destinationDir file("${buildDir}/docker-resources")
  archiveName "docker-resources.zip"
  includeEmptyDirs = false
}

tasks.compileJava.dependsOn tasks.prepareDockerBundleDir
tasks.idea.dependsOn tasks.prepareDockerBundleDir

externalPublishing {
    publication('dockerBundle') {
        artifactId 'spark-docker-resources'
        artifact tasks.prepareDockerBundleDir
    }
}

publishing {
  repositories {
    maven {
      name = "mavenTest"
      url = "${buildDir}/mavenTest"
    }
  }
}

test {
    systemProperty 'docker-resources-zip-path', "${buildDir}/docker-resources/docker-resources.zip"
    minHeapSize = "512m"
    maxHeapSize = "512m"
}

tasks.test.dependsOn tasks.publishDockerBundlePublicationToMavenTestRepository
tasks.test.environment 'MAVEN_REPO', "${buildDir.getAbsolutePath()}/mavenTest"
